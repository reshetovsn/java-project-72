plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'io.ebean' version '13.17.4'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'io.ebean:ebean-test:13.15.0'
    testImplementation 'com.konghq:unirest-java:3.14.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'

    implementation 'io.javalin:javalin:5.5.0'
    implementation 'io.javalin:javalin-rendering:5.5.0'
    implementation 'org.slf4j:slf4j-simple:2.0.5'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'com.konghq:unirest-java:3.14.5'

    implementation 'org.thymeleaf:thymeleaf:3.1.1.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.2.0' // позволяет создавать общие макеты и переиспользовать их
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE' // добавляет в шаблоны объект temporals со множеством полезных методов по форматированию
    implementation 'org.webjars:bootstrap:5.2.3'

    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.postgresql:postgresql:42.5.4'

    implementation 'io.ebean:ebean:13.15.0'
    implementation 'io.ebean:ebean-ddl-generator:13.15.0' // Позволяет генерировать миграции
    implementation 'io.ebean:ebean-querybean:13.15.0' // Query builder. Позволяет строить запросы
    implementation 'io.ebean:ebean-migration:13.7.0' // Позволяет авт выполнять миграции
    implementation 'io.ebean:ebean-annotation:8.3' // Добавляет различные аннотации
    annotationProcessor 'io.ebean:querybean-generator:13.15.0' // Обработчик аннотаций используется для генерации компонента запроса

// ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

testlogger {
    showStandardStreams true
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

application {
    mainClass.set("hexlet.code.App")
}

compileJava {
    options.release = 20
}

checkstyle {
    toolVersion '10.12.1'
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

checkstyleMain.source = "src/main/java"

checkstyleMain.exclude '**/build/generated/sources/annotationProcessor/java/main/**'